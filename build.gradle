plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.9'
}

group 'com.tabnine'
version project.hasProperty('externalVersion') ? project.externalVersion : '0.1.15'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

intellij {
    version '2019.3'
    type 'IC'
    updateSinceUntilBuild false
}

def PRODUCTION_CHANNEL = null
def channelName = project.hasProperty('channel') ? project.channel : 'alpha'
def publishChannel = channelName == "production" ? PRODUCTION_CHANNEL : channelName

sourceSets {
    main {
        java.srcDirs += "channels/${channelName}"
    }
}

def hubToken = project.hasProperty('hubToken') ? hubToken : (System.getenv('HUB_TOKEN'))

publishPlugin {
    channels publishChannel
    token hubToken
}

tasks.create("currentVersion") {
    doLast {
        println version
    }
}

task verifyJBPlugin(type: Exec) {
    workingDir projectDir.toString()
    commandLine 'ci/verify-tabnine-plugin.sh'
}
