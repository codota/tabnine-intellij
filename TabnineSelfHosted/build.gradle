import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import java.util.stream.Collectors

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.4.32"
    id 'org.jetbrains.intellij' version '1.2.0'
    id 'org.jlleitschuh.gradle.ktlint' version "10.0.0"
    id 'org.jlleitschuh.gradle.ktlint-idea' version "10.0.0"
    id "com.github.sherter.google-java-format" version "0.9"
}

repositories {
    mavenCentral()
}

group 'com.tabnineSelfHosted'
version project.hasProperty('externalVersion') ? project.externalVersion : '1.0.6'

sourceCompatibility = 9
targetCompatibility = 9

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "9"
    }
}

repositories {
    mavenCentral()
}

apply plugin: "org.jlleitschuh.gradle.ktlint-idea"

dependencies {
    implementation(project(":Common"))
    implementation(project(":TabnineSelfHostedForMarketplace"))
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.32")
}

intellij {
    version = '2019.3'
    type = 'IC'
    updateSinceUntilBuild = false
}

publishPlugin {
    enabled = false
}

tasks.create("currentVersion") {
    doLast {
        println version
    }
}

tasks.named("runIde").configure {
    if (project.hasProperty('logFilePath')) {
        systemProperty("TABNINE_LOG_FILE_PATH", logFilePath)
    }
}

tasks {
    compileKotlin {
        dependsOn(ktlintFormat)
    }
}

static def getIdeVersions(Project project) {
    def ideVersions = project.hasProperty('ideVersions') ?
            Arrays.asList(project.ideVersions.split(',')) :
            ['IC-2019.3.5']
    return ideVersions
}

runPluginVerifier {
    println ">>>> Renaming plugin.xml for runPluginVerifier to work"
    def original = layout.projectDirectory.file("../TabnineSelfHostedForMarketplace/src/main/resources/META-INF/plugin.xml").asFile
    def target = layout.projectDirectory.file("../TabnineSelfHostedForMarketplace/src/main/resources/META-INF/plugin-for-marketplace.xml").asFile
    original.renameTo(target)
    doLast {
        target.renameTo(original)
        println "<<<< Revert plugin.xml name"
    }
    ideVersions = getIdeVersions(project)
    if (project.hasProperty("localPaths")) {
        localPaths = Arrays.stream(project.localPaths.split(',')).map(File::new).collect(Collectors.toList())
    }
}